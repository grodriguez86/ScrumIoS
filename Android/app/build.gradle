def envPropertiesFile = rootProject.file("env.properties");
def envProperties = new Properties()
envProperties.load(new FileInputStream(envPropertiesFile))

apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'realm-android'

ext.versionMajor = 0
ext.versionMinor = 5
ext.versionPatch = 0
ext.versionClassifier = null
ext.isSnapshot = true
ext.minimumSdkVersion = 16

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ar.edu.uade.scrumgame"
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        minSdkVersion project.ext.minimumSdkVersion
        versionCode generateVersionCode()
        versionName generateVersionName()
        buildConfigField("String", "AMPLITUDE_API_KEY", envProperties["AMPLITUDE_API_KEY"])
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        lintOptions {
            abortOnError false
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation platform('com.google.firebase:firebase-bom:26.5.0')
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.google.dagger:dagger-android:2.24'
    implementation 'com.google.dagger:dagger-android-support:2.24'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.24'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.24'
    implementation 'com.google.code.gson:gson:2.8.6'
    testImplementation 'org.robolectric:robolectric:4.3'
    implementation 'org.mockito:mockito-core:3.1.0'
    testImplementation 'androidx.test:core:1.3.0'
    implementation 'com.jakewharton:butterknife:10.2.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.squareup.picasso:picasso:2.71828'
    //noinspection GradleDependency (Newer version only supports Kotlin)
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.github.florent37:viewtooltip:1.2.2'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation 'commons-validator:commons-validator:1.6'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.android.support:multidex:1.0.3'
    implementation project(path: ':velocity')
    implementation 'com.amplitude:android-sdk:2.25.2'
}

configurations {
    all {
        exclude module: 'commons-logging'
    }
}

private Integer generateVersionCode() {
    return ext.minimumSdkVersion * 10000000 + ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    String versionName = "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
    if (ext.versionClassifier == null && ext.isSnapshot) {
        ext.versionClassifier = "SNAPSHOT"
    }

    if (ext.versionClassifier != null) {
        versionName += "-" + ext.versionClassifier
    }
    return versionName;
}
